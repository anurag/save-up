# In order to have the shared folder during compilation
# We need to compile from the parent directory
build-directory directory:
	docker run \
		--rm \
		-it \
		-v $PWD/{{directory}}:/volume/{{directory}} \
		-v $PWD/shared:/volume/shared \
		-v cargo-cache:/root/.cargo \
		clux/muslrust:stable \
		cargo build --release --manifest-path ./{{directory}}/Cargo.toml

copy-binary directory bin_name:
	mkdir -p ./bin/{{bin_name}}/
	cp {{directory}}/target/x86_64-unknown-linux-musl/release/{{bin_name}} ./bin/{{bin_name}}/

check-graph:
	cd graphql && cargo check

build-graph:
	just build-directory graphql
	just copy-binary graphql graphql

check-mail:
	cd mail && cargo check

build-mail:
	just build-directory mail
	just copy-binary mail mail

# Use this to have a dev server
sam-local:
	sam local start-api \
		-t template.yaml \
		-p 4010 \
		--skip-pull-image

try:
	curl \
		--request POST \
		--header "Content-Type: application/json" \
		--data '{"query": "{__schema{types{name}}}", "variables": {}, "operationName": null}' \
		http://127.0.0.1:4010/graphql

invoke-migration:
	curl \
		--request POST \
		http://127.0.0.1:4010/migrate

deploy:
	sls deploy
